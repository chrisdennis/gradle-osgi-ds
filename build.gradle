plugins {
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'com.jfrog.bintray' version '1.5'
}

apply plugin: 'java'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'findbugs'
apply plugin: 'license'
apply plugin: 'com.jfrog.bintray'

group = 'org.jayware'
version = '0.1.0'

repositories {
   mavenCentral()
   jcenter()
}

dependencies {
    compile gradleApi()

    compile 'org.osgi:org.osgi.service.component.annotations:1.3.0'
    compile 'org.apache.felix:org.apache.felix.scr.generator:1.13.0'
    compile 'org.apache.felix:org.apache.felix.scr.ds-annotations:1.2.8'

    testCompile "org.testng:testng:6.9.10"
    testCompile "org.assertj:assertj-core:3.3.0"
    testCompile "org.mockito:mockito-core:1.10.19"
}

test {
    useTestNG() {}
}

findbugs {
    toolVersion = '3.0.1'
    sourceSets = [sourceSets.main]
}

license {
    header file('HEADER.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders = true
    include '**/*.java'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'gradle-osgi-ds'
        userOrg = 'jayware'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jayware/gradle-osgi-ds'
        version {
            name = project.version
            desc = "${project.description}"
            released  = new Date()
            vcsTag = project.version
        }
    }
    publications = ['GradleOsgiDS']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

plugins.withType(MavenPublishPlugin) {
    publishing {
        publications {
            GradleOsgiDS(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId project.group
                artifactId project.name
                version project.version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', "${project.group}:${project.name}")
                    root.appendNode('description', project.description)
                    root.appendNode('url', 'http://jayware.github.io/gradle-osgi-ds/')
                    root.appendNode('inceptionYear', '2016')
                    def scm = root.appendNode('scm')
                    scm.appendNode('url', 'https://github.com/jayware/gradle-osgi-ds')
                    scm.appendNode('connection', 'git@github.com:jayware/gradle-osgi-ds.git')
                    scm.appendNode('developerConnection', 'git@github.com:jayware/gradle-osgi-ds.git')
                    def developers = root.appendNode('developers')
                    def dev = developers.appendNode('developer')
                    dev.appendNode('name', 'Elmar Schug')
                    dev.appendNode('email', 'elmar.schug@jayware.org')
                    dev.appendNode('organization', 'JayWare')
                    dev.appendNode('organizationUrl', 'https://jayware.org')
                    def license = root.appendNode('licenses').appendNode('license')
                    license.appendNode('name', project.licenseName)
                    license.appendNode('url', project.licenseUrl)
                    license.appendNode('distribution', 'repo')
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}